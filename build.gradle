plugins {
  id "java"
  id "eclipse"
  id "jacoco"
  id "org.sonarqube" version "2.3"
  id 'net.nemerosa.versioning' version '2.0.0'
}

sonarqube {
    properties {
        property "sonar.projectName", "MixedReality :: Client"
        property "sonar.projectKey", "werthersechte-github:mrClient"
        property "sonar.jacoco.reportPaths", "${rootProject.buildDir}/jacoco/test.exec"
		property "sonar.projectVersion", versioning.info.full
		property "sonar.tests", "junit"
    }
}

versioning {
   scm = 'git'
}

allprojects {
    
    apply plugin: 'java'
    apply plugin: 'eclipse'
	apply plugin: 'jacoco'
    
    version = versioning.info.full
    
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://maven.repository.redhat.com/ga/"
        }
        
    }
    
    dependencies {
        compile 'org.apache.logging.log4j:log4j-api:2.7'
        compile 'org.apache.logging.log4j:log4j-core:2.7'
        compile 'net.jcip:jcip-annotations:1.0.0.redhat-8'
        
        testCompile 'junit:junit:4.12'
        testCompile 'org.mockito:mockito-core:1.+'
        testCompile 'org.powermock:powermock-mockito-release-full:1.+'
        testCompile 'org.assertj:assertj-core:3.+'
        testCompile 'org.awaitility:awaitility:3.+'
    }

    test {
        ignoreFailures = true
    }
    
}
project(':mrbotkicore'){

	apply plugin: 'application'
	mainClassName = "core.Main"

    dependencies {
        compile project(':mrbotkiessentials')
        compile project(':mrbotkinetwork')
        compile project(':mrbotkiexample')
        compile 'commons-cli:commons-cli:1.2+'
    }
    
    sourceSets {
	    main {
	        resources {
	            srcDirs = ["src/main/xml"]
	            includes = ["**/*.xml"]
	        }
	    }
	}

}

task buildBinarys(type: Copy) {
	dependsOn clean
	dependsOn project('mrbotkicore').assembleDist 
	from 'mrbotkicore/build/distributions'
    into 'dist'
}

configure(buildBinarys) {   
    group = 'build'
    description = 'Builds the binarys and deposits them in ./dist'
}

clean {
	delete 'dist/'
} 

project(':mrbotkiessentials'){

	configurations{
	    testArtifacts
	}
	
	task testJar (type: Jar) {
	    baseName = "${project.name}-test"
	    from sourceSets.test.output
	}
	
	artifacts{
	    testArtifacts testJar
	}
}
 
project(':mrbotkinetwork'){

    dependencies {
        compile project(':mrbotkiessentials')
    }

}

project(':mrbotkiremotecontrol'){

    dependencies {
        compile project(':mrbotkinetwork')
        compile project(':mrbotkiessentials')
    }

}

project(':mrbotkitools'){

    dependencies {
        compile project(':mrbotkiessentials')
		testCompile 'org.assertj:assertj-core:3.+'
        testCompile project (path: ":mrbotkiessentials", configuration: 'testArtifacts')
        
    }

}

project(':mrbotkiexample'){

    dependencies {
        compile project(':mrbotkitools')
        compile project(':mrbotkiessentials')
    }

}


